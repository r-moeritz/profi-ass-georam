   10 sys32768
   20 .opt p,oo
   30 ;
   40 ;georam output module for profi-ass
   50 ;(c) 2025 ralph moeritz. mit license
   60 ;
   70 ;--- macros ---
   80 jeq .mac adr
   90 bne .fin:jmp adr
  100 fin .men
  110 ;
  120 align .mac
  130 .if * & $000f:*= * & $fff0 + $10
  140 .men
  150 ;
  160 ;--- constants---
  170 pastrt =$80:pastop =$c0
  180 maxpag =64:maxblk =32
  190 ;
  200 ;--- os routines ---
  210 let =$a9b1:newlin =$aad7
  220 strout =$ab1e:frmnum =$ad8a
  230 comma =$aefd:facwrd =$b7f7
  240 illqua =$b248:linprt =$bdcd
  250 setlfs =$ffba:setnam =$ffbd
  260 open =$ffc0:close =$ffc3
  270 chrout =$ffd2:chkin =$ffc6
  280 clrchn =$ffcc:chrin =$ffcf
  290 ;
  300 ;--- os memory ---
  310 valtyp =$0d:intflg =$0e
  320 linnum =$14:forptr =$49
  330 status =$90
  340 ;
  350 ;--- georam registers ---
  360 georam =$de00:geopag =$dffe
  370 geoblk =$dfff
  380 ;
  390 ;--- profi-ass variables ---
  400 paop =$4b:palen =$4e
  410 paadr =$56:pabuf =$015b
  420 ;
  430 ;--- working memory (zp) ---
  440 curblk =$92:datlen =$96
  450 curpag =$a5:offset =$a7
  460 c64adr =$a8:fstpag =$f7
  470 fstblk =$f8:strlen =$f9
  480 strptr =$fa
  490 ;
  500 ;--- routines ---
  510 *=$cc00
  520 ;
  530 ;set 1st blk+pag from user input
  540 ;call from basic:
  550 ;  sys(52224) blk #,pag #
  560 set1st jsr getwrd
  570 lda linnum+1:bne iqerr
  580 lda linnum:cmp #maxblk
  590 bcs iqerr:sta fstblk
  600 jsr comma:jsr getwrd
  610 lda linnum+1:bne iqerr
  620 lda linnum:cmp #maxpag
  630 bcs iqerr:sta fstpag
  640 rts
  650 iqerr jmp illqua
  660 ;
  670 ;write object code to georam
  680 ;call from profi-ass:
  690 ;  .opt p,o=$cc30
  700 'align
  710 write lda palen
  720 cmp #pastop:beq finwrt
  730 cmp #pastrt:beq wrstrt
  740 ldy #0:ldx offset
  750 wrout lda paop,y
  760 wrout1 sta georam,x
  770 jsr incdl:inx
  780 beq wrnxpg:stx offset
  790 wrchln cpy palen
  800 beq wrfin:iny
  810 cpy #3:bcc wrout
  820 lda pabuf-3,y:jmp wrout1
  830 wrnxpg ldx #0
  840 stx offset:inc curpag
  850 lda curpag:cmp #maxpag
  860 beq wrnxbk:sta geopag
  870 jmp wrchln
  880 wrnxbk lda #0
  890 sta curpag:sta geopag
  900 inc curblk:lda curblk
  910 cmp #maxblk:beq enomem
  920 sta geoblk:jmp wrchln
  930 wrstrt jsr init
  940 ldx offset:lda paadr
  950 sta georam,x:inx
  960 lda paadr+1:sta georam,x
  970 inx:stx offset
  980 wrfin rts
  990 finwrt lda fstblk
 1000 sta geoblk:sta curblk
 1010 lda fstpag:sta geopag
 1020 sta curpag:lda datlen
 1030 sta georam:lda datlen+1
 1040 sta georam+1:jsr prntwm
 1050 rts
 1060 enomem jsr newlin
 1070 lda #<oommsg:ldy #>oommsg
 1080 jsr strout:rts
 1090 ;
 1100 ;read object code from georam
 1110 ;call from basic:
 1120 ;  sys52416
 1130 'align
 1140 read jsr readhr
 1150 inx:ldy #0
 1160 rdloop lda georam,x
 1170 sta (c64adr),y:jsr decdl
 1180 lda datlen+1:bne rdl1
 1190 lda datlen:beq rdfin
 1200 rdl1 iny:beq incadr
 1210 rdchpg inx
 1220 beq rdnxpg:jmp rdloop
 1230 incadr inc c64adr+1
 1240 jmp rdchpg
 1250 rdnxpg ldx #0
 1260 inc curpag:lda curpag
 1270 cmp #maxpag:beq rdnxbk
 1280 sta geopag:jmp rdloop
 1290 rdnxbk lda #0
 1300 sta curpag:sta geopag
 1310 inc curblk:lda curblk
 1320 cmp #maxblk:beq enomem
 1330 sta geoblk:jmp rdloop
 1340 rdfin jsr readhr
 1350 jsr printrm:rts
 1360 ;
 1370 ;load prg file from disk to georam
 1380 ;call from basic:
 1390 ;  sys(52496) filename,device #
 1400 'align
 1410 ldprg jsr getstr
 1420 lda strlen:'jeq ldfin
 1430 jsr cpyfnm:jsr comma
 1440 jsr getwrd:ldx linnum+1
 1450 bne ldfin:ldx linnum
 1460 cpx #8:bcc ldfin
 1470 lda #1:ldy #2
 1480 jsr setlfs:jsr init
 1490 lda strlen:clc
 1500 adc #6:ldx #<fnmbuf:ldy #>fnmbuf
 1510 jsr setnam:jsr open
 1520 ldx #1:jsr chkin
 1530 jsr chrin:ldx offset
 1540 sta georam,x:inx
 1550 jsr chrin:sta georam,x
 1560 inx
 1570 ldloop jsr chrin
 1580 sta georam,x:jsr incdl
 1590 lda status:bne ldfin
 1600 inx:beq ldnxpg:jmp ldloop
 1610 ldnxpg ldx #0
 1620 inc curpag:lda curpag
 1630 cmp #maxpag:beq ldnxbl
 1640 sta geopag:jmp ldloop
 1650 ldnxbl ldx #0
 1660 stx curpag:stx geopag
 1670 inc curblk:lda curblk
 1680 cmp #maxblk:beq ldnmem
 1690 sta geoblk:jmp ldloop
 1700 ldnmem jsr newlin
 1710 lda #<oommsg:ldy #>oommsg
 1720 jsr strout
 1730 ldfin lda #1
 1740 jsr close:jsr clrchn
 1750 lda fstblk:sta curblk
 1760 sta geoblk:lda fstpag
 1770 sta curpag:sta geopag
 1780 lda datlen:sta georam
 1790 lda datlen+1:sta georam+1
 1800 jsr prntwm:rts
 1810 ;
 1820 ;init georam & working memory
 1830 init lda fstblk
 1840 sta geoblk:sta curblk
 1850 lda fstpag:sta geopag
 1860 sta curpag:lda #0
 1870 sta datlen:sta datlen+1
 1880 lda #2:sta offset:rts
 1890 ;
 1900 ;increment data length
 1910 incdl inc datlen
 1920 bne finid:inc datlen+1
 1930 finid rts
 1940 ;
 1950 ;decrement data length
 1960 decdl dec datlen
 1970 lda datlen:cmp #$ff
 1980 bne findd:dec datlen+1
 1990 findd rts
 2000 ;
 2010 ;print summary of data written
 2020 prntwm jsr newlin
 2030 ldx datlen:lda datlen+1
 2040 jsr linprt:lda #<wrtmsg
 2050 ldy #>wrtmsg:jsr strout
 2060 ldx curblk:lda #0
 2070 jsr linprt:lda #","
 2080 jsr chrout:ldx curpag
 2090 lda #0:jsr linprt:rts
 2100 ;
 2110 ;print summary of data read
 2120 printrm jsr newlin
 2130 ldx datlen:lda datlen+1
 2140 jsr linprt:lda #<rdmsg
 2150 ldy #>rdmsg:jsr strout
 2160 ldx c64adr:lda c64adr+1
 2170 jsr linprt:rts
 2180 ;
 2190 ;read data length & prg address
 2200 ;from georam
 2210 readhr jsr init
 2220 ldx #0:lda georam,x
 2230 sta datlen:inx
 2240 lda georam,x:sta datlen+1
 2250 inx:lda georam,x
 2260 sta c64adr:inx
 2270 lda georam,x:sta c64adr+1:rts
 2280 ;
 2290 ;read word from basic
 2300 getwrd jsr frmnum:jmp facwrd
 2310 ;
 2320 ;read string from basic
 2330 getstr lda #$ff
 2340 sta valtyp:sta intflg
 2350 lda #<strlen:ldx #>strlen
 2360 sta forptr:stx forptr+1:jmp let
 2370 ;
 2380 ;copy string to filename buffer
 2390 cpyfnm ldx strlen
 2400 inx:inx:lda #",":sta fnmbuf,x
 2410 inx:lda #"p":sta fnmbuf,x
 2420 inx:lda #",":sta fnmbuf,x
 2430 inx:lda #"r":sta fnmbuf,x
 2440 lda strlen:tax:sec:sbc #1
 2450 tay:inx
 2460 cploop bmi cpfin
 2470 lda (strptr),y:sta fnmbuf,x
 2480 dex:dey:jmp cploop
 2490 cpfin rts
 2500 ;
 2510 ;--- tables ---
 2520 ;filename buffer
 2530 fnmbuf .asc "0:"
 2540 *= *+20
 2550 ;
 2560 ;write operation summary
 2570 wrtmsg .asc " bytes written to georam "
 2580 .byte 0
 2590 ;read operation summary
 2600 rdmsg .asc " bytes read from georam to "
 2610 .byte 0
 2620 ;
 2630 ;out of memory error message
 2640 oommsg .asc "error: georam out of memory"
 2650 .byte 0
