   10 open1,8,1,"@0:georam-out"
   20 sys32768
   30 .opt p,o1
   40 ;
   50 ;georam output module for profi-ass
   60 ;(c) 2025 ralph moeritz. mit license
   70 ;
   80 ;--- macros ---
   90 jeq .mac adr
  100 bne .fin:jmp adr
  110 fin .men
  120 ;
  130 align .mac
  140 .if * & $000f:*= * & $fff0 + $10
  150 .men
  160 ;
  170 ;--- constants---
  180 pastrt =$80:pastop =$c0
  190 maxpag =64:maxblk =32
  200 ;
  210 ;--- os routines ---
  220 let =$a9b1:newlin =$aad7
  230 strout =$ab1e:frmnum =$ad8a
  240 comma =$aefd:facwrd =$b7f7
  250 illqua =$b248:linprt =$bdcd
  260 setlfs =$ffba:setnam =$ffbd
  270 open =$ffc0:close =$ffc3
  280 chrout =$ffd2:chkin =$ffc6
  290 clrchn =$ffcc:chrin =$ffcf
  300 ;
  310 ;--- os memory ---
  320 valtyp =$0d:intflg =$0e
  330 linnum =$14:forptr =$49
  340 status =$90
  350 ;
  360 ;--- georam registers ---
  370 georam =$de00:geopag =$dffe
  380 geoblk =$dfff
  390 ;
  400 ;--- profi-ass variables ---
  410 paop =$4b:palen =$4e
  420 paadr =$56:pabuf =$015b
  430 ;
  440 ;--- working memory (zp) ---
  450 curblk =$92:datlen =$96
  460 curpag =$a5:offset =$a7
  470 c64adr =$a8:fstpag =$f7
  480 fstblk =$f8:strlen =$f9
  490 strptr =$fa
  500 ;
  510 ;--- routines ---
  520 *=$cc00
  530 ;
  540 ;set 1st blk+pag from user input
  550 ;call from basic:
  560 ;  sys(52224) blk #,pag #
  570 set1st jsr getwrd
  580 lda linnum+1:bne iqerr
  590 lda linnum:cmp #maxblk
  600 bcs iqerr:sta fstblk
  610 jsr comma:jsr getwrd
  620 lda linnum+1:bne iqerr
  630 lda linnum:cmp #maxpag
  640 bcs iqerr:sta fstpag
  650 rts
  660 iqerr jmp illqua
  670 ;
  680 ;write object code to georam
  690 ;call from profi-ass:
  700 ;  .opt p,o=$cc30
  710 'align
  720 write lda palen
  730 cmp #pastop:beq finwrt
  740 cmp #pastrt:beq wrstrt
  750 ldy #0:ldx offset
  760 wrout lda paop,y
  770 wrout1 sta georam,x
  780 jsr incdl:inx
  790 beq wrnxpg:stx offset
  800 wrchln cpy palen
  810 beq wrfin:iny
  820 cpy #3:bcc wrout
  830 lda pabuf-3,y:jmp wrout1
  840 wrnxpg ldx #0
  850 stx offset:inc curpag
  860 lda curpag:cmp #maxpag
  870 beq wrnxbk:sta geopag
  880 jmp wrchln
  890 wrnxbk sta geopag
  900 inc curblk:lda curblk
  910 cmp #maxblk:beq enomem
  920 sta geoblk:jmp wrchln
  930 wrstrt jsr init
  940 ldx offset:lda paadr
  950 sta georam,x:inx
  960 lda paadr+1:sta georam,x
  970 inx:stx offset
  980 wrfin rts
  990 finwrt lda fstblk
 1000 sta geoblk:sta curblk
 1010 lda fstpag:sta geopag
 1020 sta curpag:lda datlen
 1030 sta georam:lda datlen+1
 1040 sta georam+1:jsr prntwm
 1050 rts
 1060 enomem jsr newlin
 1070 lda #<oommsg:ldy #>oommsg
 1080 jsr strout:rts
 1090 ;
 1100 ;read object code from georam
 1110 ;call from basic:
 1120 ;  sys52416
 1130 'align
 1140 read jsr readhr
 1150 inx:ldy #0
 1160 rdloop lda georam,x
 1170 sta (c64adr),y:jsr decdl
 1180 lda datlen+1:bne rdl1
 1190 lda datlen:beq rdfin
 1200 rdl1 iny:beq incadr
 1210 rdchpg inx
 1220 beq rdnxpg:jmp rdloop
 1230 incadr inc c64adr+1
 1240 jmp rdchpg
 1250 rdnxpg ldx #0
 1260 inc curpag:lda curpag
 1270 cmp #maxpag:beq rdnxbk
 1280 sta geopag:jmp rdloop
 1290 rdnxbk sta geopag
 1300 inc curblk:lda curblk
 1310 cmp #maxblk:beq enomem
 1320 sta geoblk:jmp rdloop
 1330 rdfin jsr readhr
 1340 jsr printrm:rts
 1350 ;
 1360 ;load prg file from disk to georam
 1370 ;call from basic:
 1380 ;  sys(52496) filename,device #
 1390 'align
 1400 ldprg jsr getstr
 1410 lda strlen:'jeq ldfin
 1420 jsr cpyfnm:jsr comma
 1430 jsr getwrd:ldx linnum+1
 1440 bne ldfin:ldx linnum
 1450 cpx #8:bcc ldfin
 1460 lda #1:ldy #2
 1470 jsr setlfs:jsr init
 1480 lda strlen:clc
 1490 adc #6:ldx #<fnmbuf:ldy #>fnmbuf
 1500 jsr setnam:jsr open
 1510 ldx #1:jsr chkin
 1520 jsr chrin:ldx offset
 1530 sta georam,x:inx
 1540 jsr chrin:sta georam,x
 1550 inx
 1560 ldloop jsr chrin
 1570 sta georam,x:jsr incdl
 1580 lda status:bne ldfin
 1590 inx:beq ldnxpg:jmp ldloop
 1600 ldnxpg inc curpag
 1610 lda curpag
 1620 cmp #maxpag:beq ldnxbl
 1630 sta geopag:jmp ldloop
 1640 ldnxbl ldx #0
 1650 stx curpag:stx geopag
 1660 inc curblk:lda curblk
 1670 cmp #maxblk:beq ldnmem
 1680 sta geoblk:jmp ldloop
 1690 ldnmem jsr newlin
 1700 lda #<oommsg:ldy #>oommsg
 1710 jsr strout
 1720 ldfin lda #1
 1730 jsr close:jsr clrchn
 1740 lda fstblk:sta curblk
 1750 sta geoblk:lda fstpag
 1760 sta curpag:sta geopag
 1770 lda datlen:sta georam
 1780 lda datlen+1:sta georam+1
 1790 jsr prntwm:rts
 1800 ;
 1810 ;init georam & working memory
 1820 init lda fstblk
 1830 sta geoblk:sta curblk
 1840 lda fstpag:sta geopag
 1850 sta curpag:lda #0
 1860 sta datlen:sta datlen+1
 1870 lda #2:sta offset:rts
 1880 ;
 1890 ;increment data length
 1900 incdl inc datlen
 1910 bne finid:inc datlen+1
 1920 finid rts
 1930 ;
 1940 ;decrement data length
 1950 decdl dec datlen
 1960 lda datlen:cmp #$ff
 1970 bne findd:dec datlen+1
 1980 findd rts
 1990 ;
 2000 ;print summary of data written
 2010 prntwm jsr newlin
 2020 ldx datlen:lda datlen+1
 2030 jsr linprt:lda #<wrtmsg
 2040 ldy #>wrtmsg:jsr strout
 2050 ldx curblk:lda #0
 2060 jsr linprt:lda #","
 2070 jsr chrout:ldx curpag
 2080 lda #0:jsr linprt:rts
 2090 ;
 2100 ;print summary of data read
 2110 printrm jsr newlin
 2120 ldx datlen:lda datlen+1
 2130 jsr linprt:lda #<rdmsg
 2140 ldy #>rdmsg:jsr strout
 2150 ldx c64adr:lda c64adr+1
 2160 jsr linprt:rts
 2170 ;
 2180 ;read data length & prg address
 2190 ;from georam
 2200 readhr jsr init
 2210 ldx #0:lda georam,x
 2220 sta datlen:inx
 2230 lda georam,x:sta datlen+1
 2240 inx:lda georam,x
 2250 sta c64adr:inx
 2260 lda georam,x:sta c64adr+1:rts
 2270 ;
 2280 ;read word from basic
 2290 getwrd jsr frmnum:jmp facwrd
 2300 ;
 2310 ;read string from basic
 2320 getstr lda #$ff
 2330 sta valtyp:sta intflg
 2340 lda #<strlen:ldx #>strlen
 2350 sta forptr:stx forptr+1:jmp let
 2360 ;
 2370 ;copy string to filename buffer
 2380 cpyfnm ldx strlen
 2390 inx:inx:lda #",":sta fnmbuf,x
 2400 inx:lda #"p":sta fnmbuf,x
 2410 inx:lda #",":sta fnmbuf,x
 2420 inx:lda #"r":sta fnmbuf,x
 2430 lda strlen:tax:sec:sbc #1
 2440 tay:inx
 2450 cploop bmi cpfin
 2460 lda (strptr),y:sta fnmbuf,x
 2470 dex:dey:jmp cploop
 2480 cpfin rts
 2490 ;
 2500 ;--- tables ---
 2510 ;filename buffer
 2520 fnmbuf .asc "0:"
 2530 *= *+20
 2540 ;
 2550 ;write operation summary
 2560 wrtmsg .asc " bytes written to georam "
 2570 .byte 0
 2580 ;read operation summary
 2590 rdmsg .asc " bytes read from georam to "
 2600 .byte 0
 2610 ;
 2620 ;out of memory error message
 2630 oommsg .asc "error: georam out of memory"
 2640 .byte 0
